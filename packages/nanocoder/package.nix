{
  lib,
  buildNpmPackage,
  fetchurl,
  fetchNpmDeps,
  nodejs_20,
  runCommand,
}:

let
  version = "1.10.3";
  # First, create a source with package-lock.json included
  srcWithLock = runCommand "nanocoder-src-with-lock" { } ''
    mkdir -p $out
    tar -xzf ${
      fetchurl {
        url = "https://registry.npmjs.org/@motesoftware/nanocoder/-/nanocoder-1.10.3.tgz";
        hash = "sha256-oVrfZexq5AqHE1+H3Zd0PyPlDv1jZU30ZP8JnUN6PDw=";
      }
    } -C $out --strip-components=1
    cp ${./package-lock.json} $out/package-lock.json
  '';
in
buildNpmPackage rec {
  pname = "nanocoder";
  inherit version;

  src = srcWithLock;

  npmDeps = fetchNpmDeps {
    inherit src;
    hash = "sha256-tY4dmPDf+uN4LL+Xh4b6ODJJZueRkpUog3s0P4vvQyA=";
  };

  # The package from npm is already built
  dontNpmBuild = true;

  nodejs = nodejs_20;

  passthru = {
    updateScript = ./update.sh;
  };

  meta = with lib; {
    description = "A beautiful local-first coding agent running in your terminal - built by the community for the community âš’";
    homepage = "https://github.com/Mote-Software/nanocoder";
    changelog = "https://github.com/Mote-Software/nanocoder/releases";
    license = licenses.mit;
    sourceProvenance = with lib.sourceTypes; [ binaryBytecode ];
    maintainers = with maintainers; [ ];
    platforms = platforms.all;
    mainProgram = "nanocoder";
  };
}
